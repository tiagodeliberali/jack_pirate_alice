class Game {
    field Pirate pirate;
    field Maze maze;
    field Treasure treasure;

    constructor Game new() {
        let maze = Maze.new(4, 5, 4, 8);
        do MazeBuilder.build(maze);
        do maze.draw();

        let pirate = Pirate.new(0, 0, maze);
        let treasure = Treasure.new(maze);

        return this;
    }

    method void dispose() {
        do pirate.dispose();
        do maze.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var boolean exit;
        var char key;
        let exit = false;

        while (~exit) {
            // identify a key pressed
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }

            do pirate.move(key);
            let exit = treasure.found(pirate);

            // waits the key release
            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
            }
        }

        return;
    }
}