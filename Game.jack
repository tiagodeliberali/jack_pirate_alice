class Game {
    field Pirate pirate;
    field Maze maze;
    field Treasure treasure;

    constructor Game new(int level) {
        do Output.moveCursor(0, 0);
        do Screen.clearScreen();
        do Output.printString("Go, pirate Alice! Find the treasure!!!");

        // build maze based on level
        if (level = 1) {
            let maze = Maze.new(3, 6, 4, 8);
        }

        if (level = 2) {
            let maze = Maze.new(2, 4, 6, 12);
        }

        if (level = 3) {
            let maze = Maze.new(1, 1, 9, 19);
        }

        do MazeBuilder.build(maze);
        do maze.draw();

        let pirate = Pirate.new(0, 0, maze);
        let treasure = Treasure.new(maze);

        return this;
    }

    method void dispose() {
        do pirate.dispose();
        do maze.dispose();
        do treasure.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var boolean exit;
        var char key;
        let exit = false;

        while (~exit) {
            // identify a key pressed
            while (key = 0) {
                let key = Keyboard.keyPressed();
            }

            do pirate.move(key);
            let exit = treasure.found(pirate);

            // waits the key release
            while (~(key = 0)) {
                let key = Keyboard.keyPressed();
            }
        }

        return;
    }
}